; Archivo: POSTLAB_5
; Dispositivo: PIC16F887
; Autor: Marcela Padilla 219393
; Compilador: pic-a (v2.30), MPLABX V5.40
;
; Programa: Contador de 8 bits reflejado en Display
; Hardware: Push, LEDs, Displays
;
; Creado: 19 de febero, 2022
; Última modificación: 
Processor 16F887

 
#include <xc.inc>
    
  CONFIG FOSC=INTRC_NOCLKOUT	//oscilador interno
  CONFIG WDTE=OFF   // WDT disabled (reinicio repetitivo de pic)
  CONFIG PWRTE=OFF   // PWRT enabled (espera de 72ms al iniciar)
  CONFIG MCLRE=OFF  // El pin de MCLR se utiliza como I/O
  CONFIG CP=OFF    //Sin protección de código
  CONFIG CPD=OFF   //Sin protección de datos
  
  CONFIG BOREN=OFF  // Sin reinicio cuando el voltaje de alimentación baja de 4V
  CONFIG IESO=OFF   //Reinicio sin cambio de reloj de interno a externo
  CONFIG FCMEN=OFF  //Cambio de reloj externo a interno en caso de fallo
  CONFIG LVP=OFF	    //programación en bajo voltaje permitida
;----------------
  
UP EQU 0
DOWN EQU 1
PSECT udata_bank0
    CONTA:	DS 1	;1 BYTE
    CENTENA:	DS 1	;1 BYTE
    DECENA:	DS 1	;1 BYTE
    UNIDADES:	DS 1	;1 BYTE
    BANDERADISP:DS 1	;1 BYTE
    DISPLAY:	DS 3	;3 BYTE
  
;-------------------------------------------------------------------------------
 ;				   MACROS
 ;-------------------------------------------------------------------------------
RESETTIMER0 MACRO
    BANKSEL TMR0    ; DIRRENCIONAMIENTO AL BANCO 0
    MOVLW   217     ; CARGAR LITERAL EN EL REGISTRO
    MOVWF   TMR0    ; CONFIGURACIÓN COMPLETA PARA 10ms DE RETARDO
    BCF	    T0IF    ; SE LIMPIA LA BANDERA DE INTERRUPCIÓN
    ENDM
    
;-------------------------------------------------------------------------------
;				STATUS PARA INTERRUPCIONES
;-------------------------------------------------------------------------------
PSECT  udata_shr    ; VARIABLES GLOBALES DE MEMORIA
    WTEMP:	DS 1	; 1 BYTE
    STATUSTEMP: DS 1	; 1 BYTE
    
PSECT resVect, class = CODE, abs, delta = 2

;-------------------------------------------------------------------------------
;				  VECTOR RESET
;-------------------------------------------------------------------------------
ORG 00h
resVect: 
    PAGESEL	main	; CAMBIO DE PÁGINA 
    GOTO	main
    
PSECT intVect, class = CODE, abs, delta = 2
;-------------------------------------------------------------------------------
;				VECTOR INTERRUPCION
;-------------------------------------------------------------------------------
ORG 04h			    ; POSICIÓN 0004h PARA LAS INTERRUPCIONES
PUSH:			    ; PC A STACK
    MOVWF   WTEMP	    ; SE MUEVE W A VARIABLE WTEMP
    SWAPF   STATUS,W	    ; INTERCAMBIAR DE LOS NIBBLES Y SE ALMACENA EN W
    MOVWF   STATUSTEMP	    ; SE MUEVE A W A LA VARIABLE STATUSTEMP
ISR:			    ; RUTINA DE INTERRUPCIÓN 
    BTFSC   RBIF	    ; SI LA BANDERA DE CAMBIO DEL PORTB NO ESTA ENCENDIDA SE SALTA UNA LINEA
    CALL    INTERRUPIOCB    ; SE LLAMA RUTINA DE INTERRUPCIÓN
    BANKSEL PORTA	
    BTFSC   T0IF	    ; SI LA BANDERA DE CAMBIO TMR0 NO ESTÁ ENCENDIDA SE SALTA UNA LINEA
    CALL    INT_TMR0	    ; SE LLAMA LA RUTINA DE INTERRUPCIÓN DEL TMR0
    
POP:			    ; LA INTERRUPCIÓN SE MUEVE AL STACK DEL PC
    SWAPF   STATUSTEMP, W   ; INTERCAMBIO DE NIBBLES DE LA VARIABLE STATUSTEMP Y SE ALMACENA EN W
    MOVWF   STATUS	    ; SE MUEVE W A STATUS
    SWAPF   WTEMP, F	    ; INTERCAMBIO DE NIBBLES DE LA VARIABLE WTEMP Y SE ALMACENA EN WTEMP
    SWAPF   WTEMP, W	    ; INTERCAMBIO DE NIBBLES DE LA VARIABLE WTEMP Y SE ALMACENA EN W
    RETFIE
   
;-------------------------------------------------------------------------------
 ;				    CONFIGURACION
 ;-------------------------------------------------------------------------------
ORG 100h
main:
    CALL    CONFIGIO	    ; SE LLAMA A LA RUTINA CONFIGURACIÓN DE ENTRADAS/SALIDAS
    CALL    CONFIGRELOJ	    ; SE LLAMA A LA RUTINA CONFIGURACIÓN DE RELOJ
    CALL    CONFIGTIMER0    ; SE LLAMA A LA RUTINA CONFIGURACIÓN DE TMR0
    CALL    CONFIGINTERRUP  ; SE LLAMA A LA RUTINA CONFIGURACIÓN DE INTERRUPCIONES
    CALL    CONFIIOCB	    ; SE LLAMA A LA RUTINA CONFIGURACIÓN DE INTERRUPCIÓN EN PORTB
    BANKSEL PORTA
    
    
loop:
    CALL    VALOR_DEC		; SE LLAMA A LA RUTINA DE MOVIMIENTO DE VALORES DECIMALES
    CALL    OBTENER_CENTENAS	; SE LLAMA A LA RUTINA  PARA OBTENER LAS CENTENAS/DECENAS/UNIDADES
    GOTO    loop		; LOOP FOREVER
    
CONFIGRELOJ:
   BANKSEL OSCCON		; CONFIGURACIÓN DEL REGISTRO OSCCON PARA EL OSCILADOR
    BCF IRCF0
    BSF IRCF1
    BSF IRCF2			; OSCILADOR A 4MHZ 
    BSF SCS			; RELOJ INTERNO
    RETURN
   
CONFIGTIMER0:
    BANKSEL OPTION_REG		; SE SELECCIONA EL BANCO DEL REGISTRO DE OPCIONES
    BCF	    T0CS		; SE UTILIZA EL CICLO INTERNO
    BCF	    PSA			; PRESCALER DEL TIMER0
    BSF	    PS2			;
    BSF     PS1			;
    BSF	    PS0			; PRESCALER EN 1:256
				; N = 256-(0.01*4*10^6)/(4*256)=216.93 = 217
    RESETTIMER0			; SE LLAMA MACRO 
    
    RETURN
    
CONFIGIO:
    BANKSEL ANSEL		; DIRECCIONAL AL BANCO 11
    CLRF    ANSEL		; ENTRADAS Y SALIDAS DIGITALES
    CLRF    ANSELH		; ENTRADAS Y SALIDAS DIGITALES
    
    BANKSEL TRISA		; DIRECCIONAR AL BANCO 11
    BSF	    TRISB, UP		; RB0 COMO ENTRADA
    BSF	    TRISB, DOWN		; RB1 COMO ENTRADA
    
    CLRF    TRISA		; PORTA COMO SALIDA
    CLRF    TRISC		; PORTC COMO SALIDA
    CLRF    TRISD		; PORTD COMO SALIDA
    
    BCF	    OPTION_REG, 7	; RBPU HABILITA LAS RESISTENCIAS PULL-UP
    BSF	    WPUB, UP		; HABILITA EL REGISTRO DE PULL-UP EN RB0
    BSF	    WPUB, DOWN		; HABILITA EL REGISTRO DE PULL-UP EN RB1
    
    BANKSEL PORTA		; DIRECCIONAR AL BANCO 0
    CLRF    PORTA		; SE LIMPIA PORTA
    CLRF    PORTB		; SE LIMPIA PORTB
    CLRF    PORTC		; SE LIMPIA PORTC
    CLRF    PORTD		; SE LIMPIA PORTD	
    CLRF    CENTENA		; SE LIMPIA LA VARIABLE CENTENA
    CLRF    DECENA		; SE LIMPIA LA VARIABLE DECENA
    CLRF    UNIDADES		; SE LIMPIA LA VARIABLE UNIDADES
    CLRF    BANDERADISP		; SE LIMPIA LA VARIABLE BANDERADISP
    
    RETURN


CONFIGINTERRUP:
    BANKSEL INTCON
    BSF	    GIE		        ; HABILITA INTERRUPCIONES GLOBALES
    BSF	    RBIE		; HABILITA INTERRUPCIONES DE CAMBIO DE ESTADO DEL PORTB
    BCF	    RBIF		; SE LIMPIA LA BANDERA DE CAMBIO DEL PUERTO B
    BSF	    T0IE		; HABILITA INTERRUPCION TMRO
    BCF	    T0IF		; SE LIMPIA  LA BANDERA DE TMR0
    return
    
CONFIIOCB:
    BANKSEL TRISA
    BSF	    IOCB, UP		; INTERRUPCION CONTROL DE CAMBIO EN EL VALOR DE B
    BSF	    IOCB, DOWN		; INTERRUPCION CONTROL DE CAMBIO EN EL VALOR DE B
    
    BANKSEL PORTA
    MOVF    PORTB,W		; TERMINA LA CONDICION DE MISMATCH, COMPARA CON W
    BCF	    RBIF		; SE LIMPIA LA BANDERA  DE CAMBIO DE PORTB
    
    RETURN
    
INTERRUPIOCB:
    BANKSEL PORTA
    BTFSS   PORTB, UP		; ANALIZA RB0 SI NO ESTA PRESIONADO, SI ESTA PRESIONADO SALTA UNA LINEA
    INCF    PORTA
    BTFSS   PORTB, DOWN		; ANALIZA RB1 SI NO ESTA PRESIONADO, SI ESTA PRESIONADO SALTA UNA LINEA
    DECF    PORTA
    BCF	    RBIF		;  SE LIMPIA LA BANDERA  DE CAMBIO DE PORTB
    
    RETURN

INT_TMR0:
    RESETTIMER0			; SE REINICIA TMR0
    CALL MOSTRAR_VALORDEC	; SE LLAMA SUBRUTINA PARA LA COCNF
    RETURN

VALOR_DEC:
    MOVF    UNIDADES,W		; SE MUEVE UNIDADES A W
    CALL    TABLA		; SE LLLAMA A LA TABLA PARA CARGAR EN PORTC
    MOVWF   DISPLAY		; SE GUARDA EN NUEVA VARIABLE DISPLAY1
    
    MOVF    DECENA,W		; SE MUEVE VALOR DE DECENA A W
    CALL    TABLA		; SE LLLAMA A LA TABLA PARA CARGAR EN PORTC
    MOVWF   DISPLAY+1		; SE GUARDA EN NUEVA VARIABLE DISPLAY2
    
    MOVF    CENTENA,W		; SE MUEVE VALOR CENTENA A W
    CALL    TABLA		; SE LLLAMA A LA TABLA PARA CARGAR EN PORTC
    MOVWF   DISPLAY+2		; SE GUARDA EN NUEVA VARIABLE DISPLAY3
    RETURN

MOSTRAR_VALORDEC:
    BCF	    PORTD,0		; SE LIMPIA SE-DISPLAY DE CENTENAS
    BCF	    PORTD,1		; SE LIMPIA SET-DISPLAY DE DECENAS
    BCF	    PORTD,2		; SE LIMPIA SET-DISPLAY DE UNIDADES
    BTFSC   BANDERADISP,0	; SE VERIFICA BANDERA DISPLAY CENTENAS, SI ESTA APAGADA SALTA
    GOTO    DISPLAY3		; SI ESTA ENCENDIDA NOS MOVEMOS AL DISPLAY DE CENTENAS
    BTFSC   BANDERADISP,1	; SE VERIFICA BANDERA DISPLAY DECENA SI ESTA APAGAFA SALTA
    GOTO    DISPLAY2		; SI ESTA ENCENDIDA NOS MOVEMMOS AL DISPLAY DE DECENAS
    BTFSC   BANDERADISP,2	; SE VERIFICA BANDERA DISPLAY CENTENA SI ESTA APAGAFA SALTA
    GOTO    DISPLAY1		; SI ESTA ENCENDIDA NOS MOVEMMOS AL DISPLAY DE UNIDADES
    
DISPLAY1:
    MOVF    DISPLAY,W		; SE MUEVE EL VALOR DE UNIDADES A W
    MOVWF   PORTC		; SE MUESTRA EL DISPLAY
    BSF	    PORTD, 2		; SE ENCIENDE DISPLAY DE UNIDADES
    BCF	    BANDERADISP,2	; SE APAGA LA BANDERA DE UNIDADES
    BSF	    BANDERADISP,1	; SE ENCIENDE LA BANDERA DE DECENAS
    RETURN

DISPLAY2:
    MOVF    DISPLAY+1,W		; SE MUEVE EL VALOR DE DECENAS A W
    MOVWF   PORTC		; SE MUESTRA EL DISPLAY
    BSF	    PORTD, 1		; SE ENCIENDE DISPLAY DE DECENAS
    BCF	    BANDERADISP,1	; SE APAGA LA BANDERA DE DECENAS
    BSF	    BANDERADISP,0	; SE ENCIENDE LA BANDERA CENTENAS
    RETURN

DISPLAY3:
    MOVF    DISPLAY+2,W	    ; SE MUEVE EL VALOR DE CENTENAS A W
    MOVWF   PORTC	    ; SE MUESTRA EL DISPLAY
    BSF	    PORTD, 0	    ; SE ENCIENDE DISPLAY DE CENTENAS
    BCF	    BANDERADISP,0   ; SE APAGA LA BANDERA DE CENTENAS
    BSF	    BANDERADISP,2   ; SE ENCIENDE LA BANDERA DE UNIDADES
    RETURN

OBTENER_CENTENAS:
    CLRF    CENTENA	    ; SE LIMPIA LA BANDERA DE CENTENA
    CLRF    DECENA	    ; SE LIMPIA LA BANDERA DE DECENA
    CLRF    UNIDADES	    ; SE LIMPIA LA BANDERA DE UNIDADES
    MOVF    PORTA,W	    ; SE MUEVE EL VALOR DE PORTA A W
    MOVWF   CONTA	    ; SE MUEVE EL VALOR A W A LA VARIACION CONTA
    MOVLW   100		    ; SE MUEVE 100 A W
    SUBWF   CONTA, F	    ; SE RESTA 100 A CONTA Y SE GUARDA EN CONTA
    INCF    CENTENA	    ; SE INCREMENTA EN 1 LA VARIABLE CENTENA
    BTFSC   STATUS, 0	    ; SE VERIFICA SI ESTA APAGADA LA BANDERA DE BORROW
			    ; SI ESTA APAGADA QUIERE DECIR QUE LA RESTA OBTUVO UN VALOR NEGATIVO
			    ; SI ESTA ENCENDIDA QUIERE DECIR QUE HAY UN VALOR POSITIVO
			    
    GOTO    $-4		    ; SI ESTA ENCENDIDA SE REGRESA 4 INSTRUCCIONES ATRAS
    DECF    CENTENA	    ; SI NO ESTA ENCENDIDA SE RESTA 1 A LA VARIABLE CENTENA
    MOVLW   100		    ; SE MUEVE 100 A W
    ADDWF   CONTA, F	    ; SE AÑADEN LOS 100 A LO QUE TENGA NEGATIVO EN CONTA PARA QUE SEA POSITIVO
    CALL    OBTENER_DECENAS ; SE LLAMA A LA SUBRUTINA PARA OBTENER LAS DECENAS
    RETURN

OBTENER_DECENAS:
    MOVLW   10		    ; SE MUEVE 10 A W
    SUBWF   CONTA,F	    ; SE RESTA 10 A CONTA Y SE GUARDA EN CONTA
    INCF    DECENA	    ; SE INCREMENTA EN 1 LA VARIABLE DECENA
    BTFSC   STATUS,0	    ; SE VERIFICA SI ESTA APAGADA LA BANDERA DE BORROW
    GOTO    $-4		    ; SI NO ESTA ENCENDIDA SE REGRESAN 4 LINEAS 
    DECF    DECENA	    ; SI NO ESTA ENCENDIDA SE RESTA 1 A LA VARIABLE DECENA
    MOVLW   10		    ; SE MUEVE 10 A W
    ADDWF   CONTA, F	    ; SE AÑADEN LOS 20 A LO QUE TENGA EN ESE MOMENTO NEGATIVO EN CONTA PARA QUE SEA POSITIVO
    CALL    OBTENER_UNIDADES; SE LLAMA A LA SUBRUTINA PARA OBTNER UNIDADES
    RETURN

OBTENER_UNIDADES:
    MOVLW   1		    ; SE MUEVE 1 A W
    SUBWF   CONTA, F	    ; SE RESTA 1 A CONTA Y SE GUARDA EN CONTA
    INCF    UNIDADES	    ; SE INCREMENTA EN 1 LA VARIABLE UNIDADES
    BTFSC   STATUS, 0	    ; SE VERIFICA SI ESTA APAGADA LA BANDERA DE BORROW
    GOTO    $-4		    ; SI NO ESTA ENCENDIDA SE REGRESAN 4 LINEAS 
    DECF    UNIDADES	    ; SI NO ESTA ENCENDIDA SE RESTA 1 A LA VARIABLE UNIDADES
    MOVLW   1		    ; SE MUEVE 1 A W
    ADDWF   CONTA, F	    ; SE AÑADE 1 A LO QUE TENGA EN ESE MOMENTO NEGATIVO EN CONTA PARA QUE SEA POSITIVO 
    RETURN

;-------------------------------------------------------------------------------
 ;				    INDICE DISPLAY
 ;-------------------------------------------------------------------------------
PSECT TABLA, class = code, delta=2, abs
ORG 200h    ; posición para el código

;------------------------------------------------
TABLA:
 clrf    PCLATH
 bsf	 PCLATH, 1          ; 
 andlw	 0x0f
 addwf   PCL		    ; El valor del PC se suma con lo que haya en w para
 retlw   00111111B	    ; 0	    
 retlw   00000110B	    ; 1	    
 retlw   01011011B	    ; 2	    
 retlw   01001111B	    ; 3	    
 retlw   01100110B	    ; 4	    
 retlw   01101101B	    ; 5	    
 retlw   01111101B	    ; 6
 retlw   00000111B	    ; 7
 retlw   01111111B	    ; 8
 retlw   01100111B	    ; 9
 retlw   01110111B	    ; A
 retlw   01111100B	    ; b
 retlw   00111001B	    ; c
 retlw   01011110B	    ; d
 retlw   01111001B	    ; E
 retlw   01110001B	    ; F

END
